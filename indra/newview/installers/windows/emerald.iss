; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{204D48C5-6231-4955-83EC-623DCB437FD9}
;AppMutex=SecondLifeAppMutex
AppName=GreenLife Emerald Viewer
AppVerName=GreenLife Emerald Viewer 1.23.4 (206)
AppPublisher=ModularSystems.sl
AppPublisherURL=http://modularsystems.sl/
AppSupportURL=http://modularsystems.sl/
AppUpdatesURL=http://modularsystems.sl/
DefaultDirName={pf}\GreenLife Emerald Viewer
DefaultGroupName=GreenLife Emerald Viewer
OutputDir=C:\emerald-setup
OutputBaseFilename=GreenLifeSetup_1-23-4-206-lgg
Compression=lzma
SolidCompression=yes
PrivilegesRequired=poweruser
LicenseFile=C:\emerald-setup\license.txt
MinVersion=0,5.00sp4
SetupIconFile=C:\emerald-setup\Icons\EmeraldInstallIcon3.ico

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
WinVersionTooLowError=This program requires Windows 2000 SP4 or later.

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\emerald-run\secondlife-bin.exe"; DestDir: "{app}"; DestName: "GreenLife.exe"; Flags: ignoreversion
Source: "C:\emerald-run\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\emerald-run\*.txt"; DestDir: "{app}"; Flags: ignoreversion
;Source: "C:\emerald-run\*.manifest"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\emerald-run\app_settings\*"; DestDir: "{app}\app_settings"; Flags: ignoreversion recursesubdirs sortfilesbyextension
Source: "C:\emerald-run\character\*"; DestDir: "{app}\character"; Flags: ignoreversion recursesubdirs sortfilesbyextension
Source: "C:\emerald-run\fonts\*"; DestDir: "{app}\fonts"; Flags: ignoreversion recursesubdirs
Source: "C:\emerald-run\skins\*"; DestDir: "{app}\skins"; Flags: ignoreversion recursesubdirs sortfilesbyextension
;Source: "D:\Downloads\VS2005\vcredist_x86.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
;Source: "C:\emerald-setup\propcopy.exe"; DestDir: "{app}"; Flags: ignoreversion

[Registry]
Root: HKLM; Subkey: "Software\ModularSystems.sl"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\ModularSystems.sl\GreenLife Emerald Viewer"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\ModularSystems.sl\GreenLife Emerald Viewer\Settings"; ValueType: string; ValueName: "InstallPath"; ValueData: "{app}"

[InstallDelete]
Type: files; Name: "{app}\secondlife-bin.exe"
Type: filesandordirs; Name: "{app}\skins\dark"
Type: filesandordirs; Name: "{app}\skins\darkgred"
Type: filesandordirs; Name: "{app}\skins\darkorange"
Type: filesandordirs; Name: "{app}\skins\default"
Type: filesandordirs; Name: "{app}\skins\emerald"
Type: filesandordirs; Name: "{app}\skins\gred"
Type: filesandordirs; Name: "{app}\skins\pslgreen"
Type: filesandordirs; Name: "{app}\skins\pslpurple"
Type: filesandordirs; Name: "{app}\skins\ruby"
Type: filesandordirs; Name: "{app}\skins\sapphire"
Type: filesandordirs; Name: "{app}\skins\silves"
Type: filesandordirs; Name: "{app}\skins\white_emerald"
Type: files;          Name: "{app}\OpenJPEG.dll"
;Type: files; Name: "{app}\fmod.dll"
;Type: files; Name: "{app}\llkdu.dll"
;Type: files; Name: "{app}\SLVoice.exe"
;Type: files; Name: "{app}\vivoxsdk.dll"
;Type: files; Name: "{app}\alut.dll"
;Type: files; Name: "{app}\ortp.dll"
;Type: files; Name: "{app}\wrap_oal.dll"

[UninstallDelete]
Type: files; Name: "{app}\fmod.dll"
Type: files; Name: "{app}\llkdu.dll"
Type: files; Name: "{app}\SLVoice.exe"
Type: files; Name: "{app}\vivoxsdk.dll"
Type: files; Name: "{app}\alut.dll"
Type: files; Name: "{app}\ortp.dll"
Type: files; Name: "{app}\wrap_oal.dll"

[Icons]
Name: "{group}\GreenLife Emerald Viewer"; Parameters: "--channel ""GreenLife Emerald Viewer"" --settings settings_emerald.xml -set SystemLanguage en-us"; Filename: "{app}\GreenLife.exe"; WorkingDir: "{app}"
Name: "{group}\{cm:ProgramOnTheWeb,GreenLife Emerald Viewer}"; Filename: "http://modularsystems.sl/"
Name: "{group}\{cm:UninstallProgram,GreenLife Emerald Viewer}"; Filename: "{uninstallexe}"
;Name: "{group}\Copy proprietary files"; Filename: "{app}\propcopy.exe"; WorkingDir: "{app}"
Name: "{commondesktop}\GreenLife Emerald Viewer"; Parameters: "--channel ""GreenLife Emerald Viewer"" --settings settings_emerald.xml -set SystemLanguage en-us"; Filename: "{app}\GreenLife.exe"; WorkingDir: "{app}"; Tasks: desktopicon

[Run]
;Filename: "{tmp}\vcredist_x86.exe"; Parameters: "/Q"; Description: "Visual C++ 2005 SP1 Runtime"; StatusMsg: "Installing Visual C++ runtime";
;Filename: "{app}\propcopy.exe"; Description: "Copy proprietary files from existing Second Life installation"; WorkingDir: "{app}"; Flags: skipifsilent runascurrentuser
Filename: "{app}\GreenLife.exe"; Parameters: "--channel ""GreenLife Emerald Viewer"" --settings settings_emerald.xml -set SystemLanguage en-us"; Description: "{cm:LaunchProgram,GreenLife Emerald Viewer}"; WorkingDir: "{app}"; Flags: nowait postinstall skipifsilent

[Code]

function InitializeSetup(): Boolean;
begin
  Result := True;
  If FindWindowByClassName('Second Life') <> 0 Then
  Begin
    Result := False;
  End;
  If FindWindowByClassName('GreenLife Emerald Viewer') <> 0 Then
  Begin
    Result := False;
  End;
  If Result = False Then
  Begin
    MsgBox('Please close all instances of Second Life and GreenLife Emerald Viewer and try again', mbError, MB_OK);
  End;
end;

function InitializeUninstall(): Boolean;
begin
  Result := InitializeSetup();
end;